@synth resurfacer
longname : Juno resurfacer pad
type : synth
author : Guy Brown
version : 1.0
doc : Fat pulse width modulation pad sound with an analog feel
@end

@param cutoff
type: float
mutable: yes
step: 0.01
min: 300
max: 3000
default: 1000
doc : Controls the brightness of the sound
@end

@param volume
type: float
mutable: yes
step: 0.01
min: 0
max: 1
default: 1
doc : Controls the overall volume
@end


PULSE-OSC : vco
SQR-OSC : sub
VCA : vca
VCA : mix
VCA : volume
LPF : lowpass
SIN-OSC : lfo
VCA : pwm
SIN-OSC : filterlfo
VCA : filtermod
ADSR : env
DELAY : lag
SIN-OSC : laglfo
VCA : laggain

# connect them

vco.out -> mix.in
sub.out -> mix.in
mix.out -> lowpass.in
lowpass.out -> vca.in

vca.out -> volume.in
vca.out -> lag.in
lag.out -> volume.in
volume.out -> audio.in

laglfo.out -> laggain.in
laggain.out -> lag.lagCV

lag.lag = 0.01
laggain.level = 0.005
laglfo.pitch = 0.3

env.out -> vca.levelCV
env.attack = 0.01
env.decay = 0.1
env.release = 0.5
env.sustain = param.level*0.2
env.level = param.level

# tweaks

vco.pitch = param.pitch

lfo.pitch = 0.5
vco.pulsewidth = 0.5
lfo.out -> pwm.in
pwm.out -> vco.pulsewidthCV
pwm.level = 0
lowpass.cutoff = param.cutoff

lowpass.resonance = 5
filtermod.level = param.cutoff*0.5
filterlfo.pitch = 0.2
filterlfo.out -> filtermod.in
filtermod.out -> lowpass.cutoffCV

sub.pitch = param.pitch*0.5

volume.level = param.volume

mix.level = 0.2

# end


