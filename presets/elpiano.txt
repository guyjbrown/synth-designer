@synth elpiano
longname : Electric Piano
author : Guy Brown
version : 1.0
doc : Electronic piano roughly on the Juno-106 patch Electric Piano II
@end

@param timbre
type: float
step: 0.01
min: 0
max: 1
default: 0.5
doc: changes the timbre
@end

@param volume
type: float
step: 0.01
min: 0
max: 1
default: 0.5
doc: overall volume
@end

# modules

PULSE-OSC : vco
SIN-OSC : carrier
SIN-OSC : mod
VCA : gain
VCA : tinkle
LPF : lpf
HPF : hpf
VCA : vca
ADSR : env1
ADSR : env2
ADSR : env3
VCA : mix

# patch

vco.out -> mix.in
mix.out -> hpf.in
hpf.out -> lpf.in
lpf.out -> vca.in
vca.out -> audio.in
env1.out -> vca.levelCV
env2.out -> lpf.cutoffCV
mod.out -> gain.in
gain.out -> carrier.pitchCV
carrier.out -> tinkle.in
tinkle.out -> mix.in
env3.out -> tinkle.levelCV

# tweak

vco.pulsewidth = 0.55
hpf.cutoff = 100
lpf.resonance = 3
vco.pitch = param.pitch
gain.level = 50
tinkle.level = 0.5

carrier.pitch = map(param.pitch,3000,8000)
mod.pitch = param.pitch
mod.detune = 1329

env1.attack = 0
env1.level = param.level*param.volume
env1.decay = 0.8
env1.sustain = 0
env1.release = 0.1

env2.level = map(param.pitch,1000,2500)
env2.attack = 0
env2.decay = 0.2
env2.sustain = map(param.pitch,500,1000)
env2.release = 0.1

env3.level = map(param.timbre,0,0.8)
env3.attack = 0
env3.decay = 0.2
env3.sustain = 0.1
env3.release = 0.2

# end
