@synth twoopfm
longname : Two operator FM
type : synth
author : Guy J Brown
version : 1.0
doc : Two operator FM with carrier, modulator and feedback
@end

# parameters

@param op1_transpose
type : float
mutable: yes
step : 0.05
min : 0.25
max : 4
default : 1
doc : operator 1 transpose
@end

@param op1_fm
type : float
mutable: yes
step : 0.01
min : 0
max : 1
default : 0
doc : fm
@end

@param op1_fback
type : float
mutable: yes
step : 0.01
min : 0
max : 1
default : 0
doc : feedback
@end

@param op1_ratio
type : float
mutable: yes
step : 0.5
min : 0.5
max : 32
default : 1
doc : frequency ratio
@end

@param op1_mod_decay
type : float
mutable: yes
step : 0.01
min : 0.1
max : 10
default : 2
doc : modulator decay
@end

@param op1_car_decay
type : float
mutable: yes
step : 0.01
min : 0.1
max : 10
default : 2
doc : carrier decay
@end

@param volume
type : float
mutable: yes
step : 0.01
min : 0
max : 1
default : 0.8
doc : master volume control
@end

# modules

SIN-OSC : carrier1
SIN-OSC : modulator1
VCA : vca
VCA : mix1
VCA : feedback1
VCA : gain1
DECAY : carenv1
DECAY : modenv1

# carrier to audio 

carrier1.out -> vca.in
vca.out -> audio.in

# frequency modulation

modulator1.out -> gain1.in
gain1.out -> mix1.in
mix1.out -> carrier1.pitchCV

# feedback

carrier1.out -> feedback1.in
feedback1.out -> mix1.in

# tweak

carrier1.pitch = param.pitch*param.op1_transpose
modulator1.pitch = param.pitch*param.op1_ratio
feedback1.level = param.op1_fback*1000

# carrier envelope

carenv1.out -> vca.levelCV
carenv1.level = param.level*param.volume
carenv1.decay = param.op1_car_decay
carenv1.attack = 0.01

# modulator envelope

modenv1.out -> gain1.levelCV
modenv1.level = param.op1_fm*1000
modenv1.decay = param.op1_mod_decay
modenv1.attack = 0.01

# end
