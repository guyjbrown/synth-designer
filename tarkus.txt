@synth tarkus
longname: Tarkus rock organ
author: Guy Brown
version: 1.0
doc: Hammond organ with Keith Emerson settings
@end

# https://www.soundonsound.com/techniques/synthesizing-hammond-organ-effects

# drawbar parameter

@param drawbar
type: int
step: 1
min: 1
max: 8
default: 2
doc: level of upper harmonic
@end

@param distortion
type: float
step: 0.01
min: 0
max: 1
default: 0.2
doc: distortion
@end

# modules

SIN-OSC : vco1
SIN-OSC : vco2
SIN-OSC : vco3
SIN-OSC : vco4
SIN-OSC : perc
VCA : mix
VCA : vca
VCA : fourth
VCA : clicklevel
ADSR : env
ADSR : click
SHAPER : distort

# patching

vco1.out -> mix.in
vco2.out -> mix.in
vco3.out -> mix.in
vco4.out -> fourth.in
perc.out -> clicklevel.in
clicklevel.out -> mix.in
fourth.out -> mix.in
mix.out -> vca.in
vca.out -> distort.in
distort.out -> audio.in
env.out -> vca.levelCV
click.out -> clicklevel.levelCV

# tweaking

mix.level = 0.1
fourth.level = map(param.drawbar,0,1)
vco1.pitch = param.pitch
vco1.detune = -1200
vco2.pitch = param.pitch
vco2.detune = 700
vco3.pitch = param.pitch
vco4.pitch = param.pitch
perc.pitch = param.pitch
perc.detune = 3600
vco4.detune = 1200
distort.fuzz = map(param.distortion,0,5)

# end of file
